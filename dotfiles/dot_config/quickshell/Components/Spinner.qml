import QtQuick
import Quickshell
import qs.Settings
import "../Helpers/Utils.js" as Utils

Item {
    id: root
    
    property bool running: false
    property color color: Theme.textPrimary
    property int size: Theme.panelIconSizeSmall
    // Stroke width derived from size to avoid relying on Screen context
    property int strokeWidth: Utils.clamp(Math.round(size / 8), 1, 256)
    property int duration: Theme.uiSpinnerDurationMs
    // Allow disabling animations globally for perf testing
    property bool animationsEnabled: ((Quickshell.env("QS_DISABLE_ANIMATIONS") || "") !== "1")
    
    implicitWidth: size
    implicitHeight: size
    
    Canvas {
        id: spinnerCanvas
        anchors.fill: parent
        
        onPaint: {
            var ctx = getContext("2d")
            ctx.reset()
            
            var centerX = width / 2
            var centerY = height / 2
            var radius = Math.min(width, height) / 2 - strokeWidth / 2
            
            ctx.strokeStyle = root.color
            ctx.lineWidth = root.strokeWidth
            ctx.lineCap = "round"
            
            // Draw arc with gap (270 degrees with 90 degree gap)
            ctx.beginPath()
            ctx.arc(centerX, centerY, radius, -Math.PI/2 + rotationAngle, -Math.PI/2 + rotationAngle + Math.PI * 1.5)
            ctx.stroke()
        }
        
        property real rotationAngle: 0
        
        onRotationAngleChanged: {
            requestPaint()
        }

        NumberFadeBehavior {
            target: spinnerCanvas
            property: "rotationAngle"
            running: root.running && root.animationsEnabled
            from: 0
            to: 2 * Math.PI
            duration: root.duration
            loops: Animation.Infinite
        }
    }
} 
